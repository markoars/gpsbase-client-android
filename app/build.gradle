apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.gpsbase.client"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 41
        versionName '1.0'
        multiDexEnabled true
    }

    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"

    productFlavors {
        regular
        google
        hidden {
            applicationId "com.gpsbase.client.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {

    // Glide image library

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-compat:26.1.0'
    compile 'com.android.support:preference-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'org.osmdroid:osmdroid-android:5.6.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.4.2'
    googleCompile 'com.google.firebase:firebase-core:11.0.4'
    googleCompile 'com.google.firebase:firebase-database:11.0.4'
    googleCompile 'com.google.firebase:firebase-crash:11.0.4'

    compile 'com.firebaseui:firebase-ui-auth:2.3.0'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'

    task copyJson(type: Copy) {
        from '../../gpsbase-shared/google-services'
        into '.'
        include 'gpsbase-client-android.json'
        rename('gpsbase-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}


// Got an error when adding firebase-ui-auth. Solution:: https://github.com/evollu/react-native-fcm/issues/863
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.0.2'
            }
        }
    }
}

